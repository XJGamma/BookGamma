import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "cn.edu.xjtu.se.bookgamma"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "v0.0.0"

        String version_tag = System.getenv("TRAVIS_TAG")
        if (version_tag != null) {
            Pattern pattern = Pattern.compile("v(\\d+)\\.(\\d+)\\.(\\d+)")
            Matcher matcher = pattern.matcher(version_tag)
            if (matcher.find()) {
                String vn = matcher.group(0)
                int v1 = Integer.parseInt(matcher.group(1))
                int v2 = Integer.parseInt(matcher.group(2))
                int v3 = Integer.parseInt(matcher.group(3))
                versionCode v1 * 1000000 + v2 * 1000 + v3 + 1
                versionName vn
            }
        }

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (System.getenv("RELEASE_FLAG")) {
            release {
                storeFile rootProject.file('book_gamma.jks')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (System.getenv("RELEASE_FLAG")) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'


    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.google.code.gson:gson:2.2.+'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.squareup.okio:okio:1.8.0'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
}
